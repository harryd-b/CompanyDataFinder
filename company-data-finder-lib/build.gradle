jar {
    archiveBaseName = 'company-data-finder-lib'
    version = '0.0.1'
}

configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "$rootDir/company-data-finder-lib/src/main/java/"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "https://ws1.webservices.nl/soap_doclit?wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "org.suggs.companydatafinderlib.companyinfo.wsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencies {
    implementation libs.springBootStarterWeb
    
    testImplementation libs.snakeYaml

    compile(files(genJaxb.classesDir).builtBy(genJaxb))

    implementation "com.sun.xml.bind:jaxb-xjc:2.1.7"
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
    jaxb 'com.sun.activation:javax.activation:1.2.0'
}

